class Main {

    function void main() {
        var SpaceShip shipGame;
        var Bullet bullet;
        var Array bullets;
        var Butterfly butterfly;
        var Bat bat;
        var Fly fly;
        var char key;
        var int bulletCount;
        var int i;
        var boolean canShoot;

        let shipGame = SpaceShip.new(15, 225);
        let butterfly = Butterfly.new(20, 122);
        let bat = Bat.new(15, 30);
        let fly = Fly.new(5, 0);
        let bullets = Array.new(10); 
        let bulletCount = 0; 
        let canShoot = true;

        while (true) {
            let key = Memory.peek(24576);

            if (key = 130) {
                while (~(key = 0)) {
                    let key = Keyboard.keyPressed();
                    do shipGame.moveLeft();
                }
            }

            if (key = 132) {
                while (~(key = 0)) {
                    let key = Keyboard.keyPressed();
                    do shipGame.moveRight();
                }
            }

            if (key = 32) {
                if ((bulletCount < 10) & canShoot) {
                    let bullets[bulletCount] = Bullet.new(shipGame.getCenterX(), shipGame.getY() - 5);
                    let bulletCount = bulletCount + 1;
                    let canShoot = false;
                }
            } else {
                let canShoot = true;
            }


            if (bulletCount > 0) {
                let i = 0;
                while (i < bulletCount) {
                    let bullet = bullets[i];      
                    if (bullet.isActive()) {      
                        do bullet.move();        
                        let i = i + 1;            
                    } else {
                        do bullet.dispose();
                        let bulletCount = bulletCount - 1;
                        let bullets[i] = bullets[bulletCount];
                    }
                }
            }
            do Sys.wait(1);
            // Mover el murciÃ©lago
            do bat.move();
            do fly.move();
            do butterfly.move();
        }

        return;
    }
}
